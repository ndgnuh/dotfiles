# Maintainer: Adrian Perez de Castro <aperez@igalia.com>
pkgname='wltrunk-git'
pkgver=r50.0d6af74
pkgrel=1
license=('custom:as-is')
pkgdesc='Wayland compositor based on BSPWM'
url='https://git.sr.ht/~bl4ckb0ne/wltrunk'
arch=('x86_64')
provides=('wltrunk')
conflicts=('wltrunk')
depends=('wlroots')
makedepends=('meson' 'ninja')
source=("${pkgname}::git+${url}")
sha512sums=('SKIP')


pkgver () {
	cd "${pkgname}"
	(
		set -o pipefail
		git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' | sed 's/^version\.//' ||
		printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build () {
	cd "${pkgname}"
	rm -rf build
	meson build --prefix /usr
	ninja -C build
}

package () {
	DESTDIR="${pkgdir}" ninja -C "${pkgname}/build" install
}
